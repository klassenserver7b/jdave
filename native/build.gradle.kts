import jdave.gradle.getPlatform

plugins { `publishing-environment` }

publishingEnvironment { moduleName = "jdave-native-${getPlatform()}" }

dependencies {
    api(project(":api"))

    testImplementation(platform(libs.junit.bom))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation(libs.logback)
}

val nativeResourceRoot = "resources/libdave"

val assembleNatives by
    tasks.registering(Copy::class) {
        dependsOn(gradle.includedBuild("libdave").task(":cpp:assemble"))

        from(project.layout.projectDirectory.dir("libdave/cpp/build/libs")) {
            include {
                it.name.endsWith(".so") || it.name.endsWith(".dll") || it.name.endsWith(".dylib")
            }
        }

        into(layout.buildDirectory.dir("$nativeResourceRoot/natives/${getPlatform()}"))
    }

tasks.processResources {
    dependsOn(assembleNatives)
    from(layout.buildDirectory.dir(nativeResourceRoot))
}

tasks.test {
    useJUnitPlatform()

    jvmArgs = listOf("--enable-native-access=ALL-UNNAMED")

    testLogging { events("passed", "skipped", "failed") }

    reports {
        junitXml.required = true
        html.required = true
    }
}
